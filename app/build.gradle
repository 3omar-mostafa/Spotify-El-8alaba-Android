apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'com.google.gms.google-services'

task codeCoverageReport(type: JacocoReport) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task getCoverage(type: Exec, dependsOn: 'codeCoverageReport') {
    group = "Reporting"
    commandLine "open", "$buildDir/reports/jacoco/codeCoverageReport/html/index.html"
}

android {

    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    signingConfigs {
        release {

            def keystorePropertiesFile = rootProject.file("keystore.properties")

            if (keystorePropertiesFile.exists() && android.signingConfigs.hasProperty('release')) {

                // Initializes a new Properties() object called keystoreProperties.
                def keystoreProperties = new Properties()

                // Loads the keystore.properties file into the keystoreProperties object.
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }


    defaultConfig {
        applicationId "com.vnoders.spotify_el8alaba"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            def API_BASE_URL_RELEASE = "\"https://spotify-elghalaba.me/api/v1/\""
            buildConfigField "String", "BASE_URL", API_BASE_URL_RELEASE
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

//          Remove these two lines to disable debugging
            debuggable true
            testCoverageEnabled true
        }
        debug {
            def API_BASE_URL_DEBUG = "\"http://${InetAddress.localHost.hostAddress}:8000/api/v1/\""
            buildConfigField "String", "BASE_URL", API_BASE_URL_DEBUG
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}


dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])


    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'
    implementation "androidx.room:room-runtime:2.2.5"
    implementation 'com.google.firebase:firebase-messaging:20.1.7'
    annotationProcessor "androidx.room:room-compiler:2.2.5"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation 'com.squareup.retrofit2:retrofit-mock:2.8.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.5.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'androidx.palette:palette:1.0.0'

    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.2.2'
    implementation 'androidx.navigation:navigation-ui:2.2.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.1.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'org.mockito:mockito-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'

    implementation 'com.google.firebase:firebase-analytics:17.4.1'

    implementation 'com.facebook.android:facebook-login:5.15.3'
    implementation 'org.jetbrains:annotations:15.0'
    androidTestImplementation 'androidx.test:rules:1.1.1'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'


}
